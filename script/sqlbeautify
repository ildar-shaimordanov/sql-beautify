#!/usr/bin/perl
use strict;
use warnings;

use Getopt::Long qw(GetOptions);
use Pod::Usage;
use SQL::Beautify;


my %opt;

GetOptions(\%opt,
	'spaces=i',
	'h|help' => sub { pod2usage({ -verbose => 2}); },
);

pod2usage unless @ARGV || ! -t 0;

my $orig_sql = eval {
	local $/ = undef;
	<>;
};

my $sql = SQL::Beautify->new(%opt);
$sql->query($orig_sql);
my $nice_sql = $sql->beautify;
print $nice_sql;


=head1 NAME

Beautifier of SQL statements by adding line breaks indentation.

=head1 SYNOPSIS

    sqlbeautify --help
    sqlbeautify [options] FILEs

=head1 DESCRIPTION

The application to beautify SQL statements by adding line breaks 
indentation. It is based on the SQL::Beautify package. 

=head1 OPTIONS

=over

=item B<help>

  --help

This help.

=item B<spaces>

  --spaces 4

Number of indentation spaces (defaults to 4).

=back

=head1 COPYRIGHT

Copyright (C) 2009 by Jonas Kramer.  Published under the terms of the 
Artistic License 2.0. 

=cut
